% GRAPH=$1
% P=$2
% T0=$3
% T1=$4
property product_loyalty
is
	"$GRAPH.bcg" |= with evaluator4
macro QUERY () =
    (
	G .
	(not { SELF ... })* .
	{ SELF ?C0:String ?P0:String !"${T0}" }.
	not { SELF ... } .
	{ SELF ?C1:String ?P1:String !"${T1}" where
	    (inter (string2natset (P0), string2natset ("${P}")) <> empty)
	    and
	    (inter (string2natset (P1), string2natset ("${P}")) <> empty)
	    and
	    (inter (string2natset (P0), string2natset (P1)) <> empty)
	    and
	    (inter (string2natset (C0), string2natset (C1)) <> empty)
	}
    )
end_macro


[
    QUERY
] print (
    inter (string2natset (C0), string2natset (C1)),
    " ",
    inter (inter (string2natset (P0),string2natset (P1)), string2natset ("${P}")),
    " ",
    "${T0}",
    "\n"
);
end property

-------------------------------------------------------------------------------
-- Parse the ".log" file produced by SVL to extract the relevant output

% echo
% echo "[Customers] [Promotional products] Period"
% echo
-- Grab the lines produced by the "print" operation
-- (sets are printed in the form [a1, ..., an])
-- and delete the duplicates
% cat product_loyalty.log | grep '^[[]' | sort -u

